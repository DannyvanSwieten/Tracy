#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable

#extension GL_EXT_shader_explicit_arithmetic_types_int32 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require

#include "ray_payload.glsl"
#include "random.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 0, set = 1) uniform CameraProperties
{
  mat4 viewInverse;
  mat4 projInverse;
};

layout(binding = 1, set = 0, rgba8) uniform image2D image;
layout(location = 0) rayPayloadEXT RayPayload ray;

void main()
{
  vec3 acc = vec3(0);
  uint rayFlags = gl_RayFlagsOpaqueEXT;
  float tmin = 0.1;
  float tmax = 1000.0;

  uint pixelSeed = rand_seed(rand_seed(gl_LaunchIDEXT.x, gl_LaunchIDEXT.y), 1);
  ray.seed = pixelSeed;

  const int ssp = 4;
  for(int s = 0; s < ssp; ++s)
  {       
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + float(ssp > 1) * vec2(rand_float(pixelSeed), rand_float(pixelSeed));
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;
    d.y = -d.y;

    vec4 origin = viewInverse * vec4(0, 0, 0, 1);
    vec4 target = projInverse * vec4(d.x, d.y, 1, 1);
    vec4 direction = viewInverse * vec4(normalize(target.xyz), 0);
    vec3 color = vec3(1);

    for(int i = 0; i < 4; ++i)
    {
      traceRayEXT(topLevelAS, 
              rayFlags, 
              0xff, 
              0 /*sbtRecordOffset*/, 
              0 /*sbtRecordStride*/,
              0 /*missIndex*/, 
              origin.xyz, tmin, 
              direction.xyz, tmax, 
              0 /*payload index*/);

      color *= ray.color.rgb;
      if(!ray.hit)
        break;

      // if(rand_float(pixelSeed) < dot(color, color) && i > 4)
      //   break;

      origin.xyz = ray.point + ray.w_out * .05;
      direction.xyz = ray.w_out;
    }

    acc += color;
  }

  imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(pow(acc / ssp, vec3(1) / 2.2), 1));
}